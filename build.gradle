plugins {
    id 'org.springframework.boot' version '2.4.4'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'com.google.protobuf' version '0.8.15'
    id 'idea'
}

group = 'com.bnpinnovation'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    // https://mvnrepository.com/artifact/com.google.protobuf/protobuf-java
    implementation group: 'com.google.protobuf', name: 'protobuf-java', version: '3.15.7'
    implementation "com.google.protobuf:protobuf-java-util:3.15.7"

    compile group: 'io.grpc', name: 'grpc-netty-shaded', version: '1.36.1'
    compile group: 'io.grpc', name: 'grpc-protobuf', version: '1.36.1'
    compile group: 'io.grpc', name: 'grpc-stub', version: '1.36.1'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

test {
    useJUnitPlatform()
}

protobuf {
    // Configure the protoc executable
    generatedFilesBaseDir = "$projectDir/gen"
    protoc {
        // Download from repositories
        artifact = 'com.google.protobuf:protoc:3.15.7'
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:1.36.1"
        }
    }
    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

clean {
    delete protobuf.generatedFilesBaseDir
}

sourceSets {
    main {
        java {
            srcDirs '${protobuf.generatedFilesBaseDir}/main/grpc'
            srcDirs '${protobuf.generatedFilesBaseDir}/main/java'
        }
    }
}

idea {
    module {
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/java")
        sourceDirs += file("${protobuf.generatedFilesBaseDir}/main/grpc")
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs << '-Xlint:unchecked'
    options.deprecation = true
}